plugins {
  id 'java-library'
  id 'maven-publish'
  id 'signing'

  // Format java code with spotless via prettier-java
  id 'com.diffplug.spotless' version '6.10.0'

  // delombok sources
  id 'io.freefair.lombok' version '6.5.1'

  // publishToSonatype and closeAndReleaseSonatypeStagingRepository tasks
  id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

group = 'io.github.mschout'
description = 'mail-srs-java'

repositories {
  mavenLocal()
  maven {
    url = uri('https://repo.maven.apache.org/maven2/')
  }
}

dependencies {
  implementation 'com.google.guava:guava:31.1-jre'
  testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(8))
  }

  withSourcesJar()
  withJavadocJar()
}

test {
  useJUnitPlatform()
}

// Format java code with spotlessApply task
spotless {
  java {
    prettier(['prettier': '2.0.5', 'prettier-plugin-java': '0.8.0'])
      .config([
        'parser': 'java',
        'tabWidth': 2,
        'printWidth': 140,
        'trailingComma': 'none',
        'useTabs': false
      ])
  }
}

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
  options.addStringOption('Xdoclint:none', '-quiet')
}

signing {
  useGpgCmd()
  sign publishing.publications
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId    = 'io.github.mschout'
      artifactId = 'mail-srs-java'
      version    = '0.9.1'

      from components.java

      pom {
        name = "${groupId}:${artifactId}"
        description = 'Interface to Email Sender Rewriting Scheme for Java'
        url = 'https://github.com/mschout/mail-srs-java'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            name = 'Michael Schout'
            email = 'schoutm@gmail.com'
            organizationUrl = 'https://github.com/mscnout'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/mschout/mail-srs-java.git'
          developerConnection = 'scm:git:ssh://github.com:mschout/mail-srs-java.git'
          url = 'https://github.com/mschout/mail-srs-java/tree/master'
        }
      }
    }
  }
}

nexusPublishing {
  repositories {
    sonatype {
      nexusUrl = uri("https://s01.oss.sonatype.org/service/local/")
      snapshotRepositoryUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
      username = System.getenv("OSSRH_USERNAME") ?: 'credentials'
      password = System.getenv("OSSRH_PASSWORD") ?: 'credentials'
    }
  }
}

// vim: ft=groovy
